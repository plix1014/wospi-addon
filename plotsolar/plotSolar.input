# DAILY MAX UV and SOLAR RADIATION OVER A 12-MONTH PERIOD
#
# Rev. 20141228/TMJ  -  tmj@bitwrap.no
# http://annoyingdesigns.com
#
# This script file can be modified, but please be careful.


# RETRIEVE GPVAL_Y_MIN and GPVAL_Y_MAX after imaginary plot of solar radiation
set terminal unknown
set xdata time
set timefmt '%Y.%m.%d'
set datafile separator ','

plot '/var/tmp/plotsolar.tmp' using 1:3 axis x1y1

min_y = GPVAL_DATA_Y_MIN
max_y = GPVAL_DATA_Y_MAX


# Count number of records
unset xdata
stats '/var/tmp/plotsolar.tmp' using 1
set xdata time

reset



# PERFORM THE ACTUAL PLOT
set terminal pngcairo enhanced size 1080, 480
set encoding utf8
set output '/var/tmp/plotsolar.png' 

set ylabel sprintf("Watts / m^{2}")
set y2label "UV Index"

set title sprintf("PLOTTITLE\nTIMESTAMP")

set key right below

set xtics rotate
set xtics font 'Helvetica, 9'
set ytics nomirror
set y2range[0:16]
set y2tics

set xdata time
set timefmt '%Y.%m.%d'
set datafile separator ','
set format x sprintf("%d.%m.%Y")
set autoscale fix


# Adjust number of X labels
xT = 1
if(STATS_records < 25) xT = 1; else if (STATS_records < 50) xT = 2; else if (STATS_records < 100) xT = 4; else if(STATS_records < 200) xT = 10; else xT = 15
set xtics 86400 * xT


# Calculate mean_y (could as well use the STATS data)
set fit logfile '/var/tmp/fit.log'
f(x) = mean_y
fit f(x) '/var/tmp/plotsolar.tmp' u 1:($3) via mean_y 

set label 1 sprintf("LOWEST DAILY MAX = %d watts/m^{2}   HIGHEST DAILY MAX = %d watts/m^{2}   AVERAGE DAILY MAX = %d watts/m^{2} over a period of %d days", min_y,  max_y, mean_y, STATS_records) center at graph(1, 0.5), graph(1,1.04) textcolor rgb 'black' font 'Helvetica, 10'

set grid front lt 8

plot min_y-2 with filledcurves y1=mean_y*2 lc rgb '#228b22' title 'Average daily max solar radiation', \
max_y+2 with filledcurves y1=mean_y lc rgb '#afeeee' notitle, \
'/var/tmp/plotsolar.tmp' u 1:3 with filledcurves x1 lc rgb '#ffd500' title sprintf("Daily max solar radiation in watts/m^{2}"), \
'' u 1:3 with lines lc rgb 'black' notitle, \
'' u 1:2 with filledcurves x1 lc rgb '#ff4500' axis x1y2 title "UV Index [0, 16]", \
'' u 1:2 with lines lc rgb 'black' axis x1y2 notitle, \
'' u 1:3:(1.0) smooth bezier axis x1y1 lc rgb '#ff1493' lw 3 title "Bezier approximation of solar radiation"

 


