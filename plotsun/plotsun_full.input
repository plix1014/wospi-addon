# DAILY SUNSET, SUNRISE and DAYLIGHT HOUR OVER A 12-MONTH PERIOD
#
# Rev. 20160121/PLI
# based on script from tmj@bitwrap.no
# http://annoyingdesigns.com
#
# file variables
tmpdir  = '/var/tmp/'
datain  = tmpdir . 'plotsun_full.tmp'
dataout = tmpdir . 'plotsun_full.png'

SUNRISE = system("head -1 ".tmpdir."sunrise.tmp | awk '{print $2}'")
SUNSET  = system("head -1 ".tmpdir."sunset.tmp  | awk '{print $2}'")
SUNHH   = system("tail -1 ".datain." | nawk -F\",\" '{gsub(/^[ ]+/,\"\",$3); print $3}'")

if (GPVAL_VERSION >= 5.0) {set colors classic}
set label sprintf("^{Sunrise}") center at SUNRISE, graph(1,1.04)
set label sprintf("^{Sunset}") center at SUNSET, graph(1,1.04)

# Count number of records
unset xdata
stats datain using 1
set xdata time
reset

# 
set terminal unknown
set xdata time
set timefmt '%d.%m.%Y'
set datafile separator ','

plot datain using 1:3 axis x1y1

# define the plot bounderies
min_y = 0
max_y = 24

reset


# PERFORM THE ACTUAL PLOT
set terminal pngcairo enhanced size 1080, 480
set encoding utf8
set output dataout

set ylabel sprintf("Sunrise/Sunset h")
set y2label "Sunshine h"

set title sprintf("PLOTTITLE\nTIMESTAMP")

set key right below

set xtics rotate
set xtics font 'Helvetica, 9'
set yrange [0:24]
set y2range [0:24]
set ytics 2
set ytics nomirror
set y2tics 4

set xdata time
set timefmt '%d.%m.%Y'
set datafile separator ','
set format x sprintf("%d.%m.%Y")
set autoscale fix


# Adjust number of X labels
xT = 1
if(STATS_records < 25) xT = 1; else if (STATS_records < 50) xT = 2; else if (STATS_records < 100) xT = 4; else if(STATS_records < 200) xT = 10; else xT = 15
set xtics 86400 * xT

set label 1 sprintf("TODAY SUNRISE: %s / SUNSET: %s  DAYLIGHT HOURS: %s    Curves for %d days worth of data", SUNRISE, SUNSET, SUNHH, STATS_records) center at graph(1, 0.5), graph(1,1.06) textcolor rgb 'black' font 'Helvetica, 8'

set style line 12 lc rgb '#808080' lt 0 lw 1
set grid front ls 12

set style fill solid 1.00 noborder
set style line 1 lc rgb '#ff1493' lw 2
set style line 2 lc rgb '#DC143C' lw 2
set style line 3 lc rgb '#1493ff' lw 2
set style line 5 lc rgb '#33cc33' lw 2

# get the equinox and solstice dates
SPRING = system("awk '{print $1}' ".tmpdir."equinox.tmp")
SUMMER = system("awk '{print $2}' ".tmpdir."equinox.tmp")
AUTUMN = system("awk '{print $3}' ".tmpdir."equinox.tmp")
WINTER = system("awk '{print $4}' ".tmpdir."equinox.tmp")

set label sprintf("^{Spring}") center at SPRING, graph(1,1.02) font 'Verdana, 7'
set label sprintf("^{Summer}") center at SUMMER, graph(1,1.02) font 'Verdana, 7'
set label sprintf("^{Autumn}") center at AUTUMN, graph(1,1.02) font 'Verdana, 7'
set label sprintf("^{Winter}") center at WINTER, graph(1,1.02) font 'Verdana, 7'
set label sprintf("^{Noon}")   at graph(0,0.04),0.53 tc rgb '#cc0099' font 'Verdana, 9' front

set arrow from SPRING,graph(0,0) to SPRING,graph(1,1) nohead ls 4 lw 2 front
set arrow from SUMMER,graph(0,0) to SUMMER,graph(1,1) nohead ls 4 lw 2 front
set arrow from AUTUMN,graph(0,0) to AUTUMN,graph(1,1) nohead ls 4 lw 2 front
set arrow from WINTER,graph(0,0) to WINTER,graph(1,1) nohead ls 4 lw 2 front
set arrow from graph(0,0),0.5    to graph(1,1),0.5    nohead ls 4 lw 2 front

# NOTE: ---------- PLOT ---------- PLOT ---------- PLOT ----------

# plot charts
plot datain u 1:3 w filledcurves x2 lc rgb "#000066" notitle, \
'' u 1:2 w filledcurves x1 lc rgb "#000066" notitle, \
'' u 1:2:3 w filledcurves lc rgb "#ffffb3" notitle, \
'' u 1:2 ls 2 with lines axis x1y1 title "Sunrise", \
'' u 1:3 ls 3 with lines axis x1y1 title "Sunset", \
'' u 1:4 with lines ls 5 axis x1y2 title "Daylight"

# NOTE: ---------- PLOT ---------- PLOT ---------- PLOT ----------

