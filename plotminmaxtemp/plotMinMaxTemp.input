# MIN/MAX TEMPERATURE OVER A 12-MONTH PERIOD
#
# Rev. 20140104/TMJ  -  tmj@bitwrap.no
# http://annoyingdesigns.com
#
# This script file can be modified, but please be careful.


# SUPPORT FUNCTION TO CONVERT DEGREES C TO DEGREES F
toF(temp) = (temp * 1.8) + 32


# RETRIEVE GPVAL_Y_MIN and GPVAL_Y_MAX after imaginary plot
# Scale Y2 axis for temperature in deg. F
set terminal dumb
set xdata time
set timefmt '%Y.%m.%d'
set datafile separator ','

plot '/var/tmp/plotminmax.tmp' using 1:2 axis x1y1, \
'' using 1:3 axis x1y1

min_y = GPVAL_DATA_Y_MIN
max_y = GPVAL_DATA_Y_MAX
Y2MIN = toF(GPVAL_Y_MIN)
Y2MAX = toF(GPVAL_Y_MAX)

# Count number of records
unset xdata
stats '/var/tmp/plotminmax.tmp' using 2:3
set xdata time

reset



# PERFORM THE ACTUAL PLOT
set terminal pngcairo enhanced size 1080, 480
set encoding utf8
set output '/var/tmp/plotminmax.png' 

set ylabel "°C"
set y2label "°F"

set title sprintf("PLOTTITLE\nTIMESTAMP")

set nokey

set xtics rotate
set xtics font 'Helvetica, 9'
set ytics nomirror
set y2range[Y2MIN:Y2MAX]
set y2tics

set xdata time
set timefmt '%Y.%m.%d'
set datafile separator ','
set format x sprintf("%d.%m.%Y")
set autoscale fix


# Add some "room" to the graph (-2, +2)
set yrange[min_y-2:max_y+2]

# Adjust number of X labels
xT = 1
if(STATS_records < 25) xT = 1; else if (STATS_records < 50) xT = 2; else if (STATS_records < 100) xT = 4; else if(STATS_records < 200) xT = 10; else xT = 15
set xtics 86400 * xT


# Calculate mean_y (could as well use the STATS data)
set fit logfile '/var/tmp/fit.log'
f(x) = mean_y
fit f(x) '/var/tmp/plotminmax.tmp' u 1:($2) via mean_y 

set label 1 sprintf("MIN = %#1.1f°C (%#1.1f°F)   MAX = %#1.1f°C (%#1.1f°F)   AVERAGE = %#1.1f°C (%#1.1f°F) over a period of %d days", min_y, toF(min_y), max_y, toF(max_y), mean_y, toF(mean_y), STATS_records) center at graph(1, 0.5), graph(1,1.04) textcolor rgb 'black' font 'Helvetica, 10'

set xlabel textcolor rgb 'red'
set xlabel offset 0,-1.2
set xlabel font 'Helvetica, 10'
#set xlabel 'This temperature plot is updated at midnight and at noon.'

set grid front lt 8

plot min_y-2 with filledcurves y1=mean_y lc rgb '#87cefa' notitle, \
max_y+2 with filledcurves y1=mean_y lc rgb '#f5f5f5' notitle, \
'/var/tmp/plotminmax.tmp' u 1:3 with filledcurves x1 lc rgb '#c71585', \
'' u 1:3 with lines lc rgb 'black' notitle, \
'' u 1:2 with filledcurves x1 lc rgb 'steelblue', \
'' u 1:2 with lines lc rgb 'black' notitle


