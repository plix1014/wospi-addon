# PLOT NUMBER OF RAINY DAYS PER MONTH
#
# Rev. 20150701/TMJ  -  tmj@bitwrap.no
# http://annoyingdesigns.com
#
# This script file can be modified, but please be careful.


set terminal pngcairo enhanced size 1080, 480
set encoding utf8
set output '/var/tmp/monthlyraindays.png'
set xdata time
set timefmt '%Y-%m'
set datafile separator ','
set style histogram
set title sprintf("PLOTTITLE\nTIMESTAMP")
YMIN = 0
YMAX = 34  # make it 34 to always have room for value above each plotted bar

set grid
set key off

set ylabel 'Rainy days'
set xlabel textcolor rgb 'red'
set xlabel offset 0,-1.2
set xlabel font 'Helvetica, 10'

set xlabel 'NOTE: Rainfall data only correct in non-freezing temperature conditions. Daily rainfall of > RAINTHRESHOLDTEXT considered a rainy day.'

set bmargin at screen 0.22
set yrange[YMIN:YMAX]
set xtics rotate
set xtics font 'Helvetica, 12'
set format x '%m.%Y'
set style fill transparent solid 0.5 border -1

seconds_per_day = 60 * 60 * 24
set xtics seconds_per_day * 30
set auto fix


set label 1 sprintf("Rainfall data recordings valid from COMMISSIONDATE") center at graph(1, 0.5), graph(1,1.04) textcolor rgb 'black' font 'Helvetica, 10'

plot '/var/tmp/monthlyRain.tmp' using 1:3 with boxes lc rgb 'steelblue', '' using 1:3:3 with labels textcolor rgb 'black' center offset 0,0.6 notitle


